program :               function_decl | function_decl program

empty:

function_decl:          "function" ID LPAREN function_args RPAREN compound_statement
function_args:          empty | function_args_list
function_args_list:     ID | ID COMMA function_args_list

compound_statement:     LBRACE empty RBRACE | LBRACE statement_list RBRACE
statement_list:         statement | statement SEMI statement_list
statement:              expr | assignment | if_block | return_statement

expr:                   arithmetic ((LT | LE | EQ | GE | GT) arithmetic)*
arithmetic:             term ((PLUS | MINUS) term)*
term:                   factor ((MUL | DIV) factor)*
factor:                 PLUS factor
                        | MINUS factor
                        | INTEGER
                        | LPAREN expr RPAREN
                        | variable
                        | function_call
                        | STRING

assignment:             ID ASSIGN expr

if_block:               IF LPAREN expr RPAREN compound_statement |  IF LPAREN expr RPAREN compound_statement else_block
else_block:             ELSE compound_statement | ELSE if_block

return_statement:       RETURN expr

function_call:         ID LPAREN call_args RPAREN
call_args:             empty | call_args_list
call_args_list:        expr | expr COMMA call_args_list

variable: ID